plugins {
    id 'war'
    id "com.diffplug.spotless" version "6.25.0"
}
description = 'Smoothness demo app'
group 'org.jlab'
version rootProject.version
// In this monorepo we use:
ext.smoothnessVersion = rootProject.version
// External projects should declare like so:
//ext.smoothnessVersion = '4.12.1'
ext.releaseDate = rootProject.ext.releaseDate

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

dependencies {
    // Wildfly 26.1.2.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2',
                    'org.hibernate:hibernate-core:5.3.28.Final'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
                    'org.jlab:jlog:5.0.0',
                    'org.keycloak:keycloak-admin-client:20.0.5',
                    'org.apache.poi:poi:5.2.3',
                    'org.apache.poi:poi-ooxml:5.2.3'

    // weblib should be included in war: https://github.com/JeffersonLab/smoothness/issues/4

    // For monorepo only use:
    implementation project(':smoothness-weblib')

    // In separate external projects use:
    //implementation "org.jlab:smoothness-weblib:${smoothnessVersion}"
}


war {
    archiveFileName = 'smoothness-demo.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@SMOOTHNESS_VERSION@", smoothnessVersion)
        }
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}