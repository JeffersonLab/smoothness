plugins {
    id 'war'
    id "com.diffplug.spotless" version "7.2.1"
}
description = 'Smoothness demo app'
group = 'org.jlab'
version = rootProject.version
ext.version = project.version
// In this monorepo we use:
ext.smoothnessVersion = rootProject.version
// External projects should declare like so:
//ext.smoothnessVersion = '4.12.1'
ext.releaseDate = rootProject.ext.releaseDate

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
repositories {
    mavenCentral()
}
dependencies {
    // Wildfly 37.0.0.Final provides:
    compileOnly 'jakarta.platform:jakarta.jakartaee-api:10.0.0',
                'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.2',
                'org.hibernate:hibernate-core:6.6.19.Final',
                'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.0' // Impl may vary, declaration helps IntelliJ find taglib URI

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:5.1.3',
                    'org.jlab:jlog:5.2.0',
                    'org.keycloak:keycloak-admin-client:26.0.6',
                    'org.apache.poi:poi:5.4.1',
                    'org.apache.poi:poi-ooxml:5.4.1'

    // weblib should be included in war: https://github.com/JeffersonLab/smoothness/issues/4

    // For monorepo only use:
    implementation project(':smoothness-weblib')

    // In separate external projects use:
    //implementation "org.jlab:smoothness-weblib:${smoothnessVersion}"
}
tasks.named('jar') {
    enabled = false
}
war {
    archiveFileName = 'smoothness-demo.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@SMOOTHNESS_VERSION@", smoothnessVersion)
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}