plugins {
    id 'war'
}
description = 'Smoothness demo app'
group 'org.jlab'
version rootProject.version
ext.releaseDate = rootProject.ext.releaseDate

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

dependencies {
    if (project.hasProperty("provided")) {
        providedCompile project(':smoothness-weblib'),
                'org.tuckey:urlrewritefilter:4.0.4',
                'org.hibernate.orm:hibernate-core:6.1.3.Final',
                'org.jlab:jlog:5.0.0'
    } else {
        implementation project(':smoothness-weblib'),
                'org.tuckey:urlrewritefilter:4.0.4',
                'org.hibernate.orm:hibernate-core:6.1.3.Final',
                'org.jlab:jlog:5.0.0'
    }

    // No matter what the Java EE API is provided by the application server
    providedCompile 'javax:javaee-api:8.0.1'
}

war {
    archiveFileName = 'smoothness-demo.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}