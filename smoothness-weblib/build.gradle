plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.gradlewebtools.minify' version '1.3.1'
    id 'org.jlab.tlddoc'
}

description 'Smoothness web template and JSP tag library'
group 'org.jlab'
version rootProject.version
ext.releaseDate = rootProject.ext.releaseDate

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // Smoothness setup should install this into Wildfly
    compileOnly 'org.keycloak:keycloak-admin-client:20.0.5'

    // Wildfly 26.1.2.Final provides:
    compileOnly 'javax:javaee-api:8.0.1',
                'javax.servlet:jstl:1.2'
                //'org.apache.taglibs:taglibs-standard-compat:1.2.6-RC1'
}

javadoc {
    options.overview = 'src/main/java/overview.html'
    options.source = 17
    options.with {
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
    }
}

javadoc.finalizedBy(tlddoc)

task cssMinifyIt(type: org.gradlewebtools.minify.CssMinifyTask) {
    srcDir = project.file('src/main/resources/META-INF/resources/resources/css')
    dstDir = project.file('build')
    options {
        ignoreMinFiles = true
    }
}

task jsMinifyIt(type: org.gradlewebtools.minify.JsMinifyTask) {
    srcDir = project.file('src/main/resources/META-INF/resources/resources/js')
    dstDir = project.file('build')
    options {
        ignoreMinFiles = true
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications
}

publishing {
    repositories {
        maven {
            name 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('ossrh.user') ?: System.getenv('OSSRH_USER')
                password = project.findProperty('ossrh.token') ?: System.getenv('OSSRH_TOKEN')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/JeffersonLab/smoothness'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/JeffersonLab/smoothness/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'slominskir'
                        name = 'Ryan Slominski'
                        email = 'ryans@jlab.org'
                    }
                }
                scm {
                    url = 'https://github.com/JeffersonLab/smoothness.git'
                }
            }
        }
    }
    artifacts {
        archives javadocJar, sourcesJar
    }
}