plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "1.8.5"
    id "com.eriwen.gradle.css" version "2.14.0"
    id "eu.butter.gradle.js" version "2.15.1"
}

project.sourceCompatibility = JavaVersion.VERSION_11
project.targetCompatibility = JavaVersion.VERSION_11
description 'Smoothness web template and JSP tag library'
group 'org.jlab'
version '2.23.0'
ext.releaseDate = 'Jan 11 2021'

repositories {
    jcenter()
}

dependencies {
    api 'javax:javaee-api:7.0',
         'javax.servlet:jstl:1.2'
}

minifyCss {
    source = "${projectDir}/src/main/resources/META-INF/resources/resources/css/smoothness.css"
    dest = file("${projectDir}/build/smoothness.min.css")
}

minifyJs {
    source = "${projectDir}/src/main/resources/META-INF/resources/resources/js/smoothness.js"
    dest = file("${projectDir}/build/smoothness.min.js")
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "slominskir"
            name "Ryan Slominski"
            email "ryans@jlab.org"
        }
    }

    scm {
        url "https://github.com/JeffersonLab/smoothness"
    }
}

publishing {
    publications {
        ProjectPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['ProjectPublication']

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/JeffersonLab/smoothness.git'
        version {
            name = project.version
        }
    }
}