buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://build.acc.jlab.org/artifactory/plugins-release'
        }

    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id "com.eriwen.gradle.css" version "2.14.0"
    id "eu.butter.gradle.js" version "2.15.1"
}

project.sourceCompatibility = JavaVersion.VERSION_11
project.targetCompatibility = JavaVersion.VERSION_11
group 'org.jlab'
version '2.22.0'

// Better set defaults instead of crashing build! (Publish won't work unless set correctly)
ext.artifactory_user = project.getProperties().getOrDefault("artifactory_user", "anonymous")
ext.artifactory_password = project.getProperties().getOrDefault("artifactory_password", "anonymous")

repositories {
    jcenter()
}

dependencies {
    api 'javax:javaee-api:7.0',
         'javax.servlet:jstl:1.2'
}


allprojects {
    apply plugin: "com.jfrog.artifactory"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

minifyCss {
    source = "${projectDir}/src/main/resources/META-INF/resources/resources/css/smoothness.css"
    dest = file("${projectDir}/build/smoothness.min.css")
}

minifyJs {
    source = "${projectDir}/src/main/resources/META-INF/resources/resources/js/smoothness.js"
    dest = file("${projectDir}/build/smoothness.min.js")
}

artifactory {
    contextUrl = "http://build.acc.jlab.org/artifactory"
    publish {
        repository {
            repoKey = 'gradle-release-local'
            maven = true
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-dev'
            maven = true

        }
    }
}