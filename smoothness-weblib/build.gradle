plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id ("org.gradlewebtools.minify") version "1.3.1"
}

apply plugin: org.jlab.TagLibDocPlugin

description 'Smoothness web template and JSP tag library'
group 'org.jlab'
version rootProject.version
ext.releaseDate = rootProject.ext.releaseDate

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'javax.servlet:jstl:1.2'
    implementation 'org.keycloak:keycloak-admin-client:19.0.2'
    compileOnly 'javax:javaee-api:8.0.1'
}

javadoc {
    options.overview = "src/main/java/overview.html"
    options.source = 17
    options.with {
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
    }
}

task cssMinifyIt(type: org.gradlewebtools.minify.CssMinifyTask) {
    srcDir = project.file("src/main/resources/META-INF/resources/resources/css")
    dstDir = project.file("build")
    options {
        ignoreMinFiles = true
    }
}

task jsMinifyIt(type: org.gradlewebtools.minify.JsMinifyTask) {
    srcDir = project.file("src/main/resources/META-INF/resources/resources/js")
    dstDir = project.file("build")
    options {
        ignoreMinFiles = true
    }
}

signing {
    sign publishing.publications
}

publishing {
    repositories {
        maven {
            name "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("ossrh.user") ?: System.getenv("OSSRH_USER")
                password = project.findProperty("ossrh.token") ?: System.getenv("OSSRH_TOKEN")
            }
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/JeffersonLab/jaws-libj")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/JeffersonLab/smoothness"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/JeffersonLab/jaws-libj/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "slominskir"
                        name = "Ryan Slominski"
                        email = "ryans@jlab.org"
                    }
                }
                scm {
                    url = "https://github.com/JeffersonLab/smoothness.git"
                }
            }
        }
    }
    artifacts {
        archives javadocJar, sourcesJar
    }
}