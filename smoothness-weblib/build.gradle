plugins {
    id 'java-library'
    id 'org.gradlewebtools.minify' version '2.1.1'
    id 'org.jlab.tlddoc'
    id "com.diffplug.spotless" version "7.2.1"
    id "com.vanniktech.maven.publish" version "0.34.0"
}

description = 'Smoothness web template and JSP tag library'
group = 'org.jlab'
version = rootProject.version
ext.releaseDate = rootProject.ext.releaseDate

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}
repositories {
    mavenCentral()
}
dependencies {
    // Smoothness setup should install this into Wildfly
    compileOnly 'org.keycloak:keycloak-admin-client:26.0.6'

    // Wildfly 37.0.1.Final provides:
    compileOnly 'jakarta.platform:jakarta.jakartaee-api:10.0.0',
                'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'
}
javadoc {
    options.overview = 'src/main/java/overview.html'
    options.source = 17
    options.with {
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
    }
}
javadoc.finalizedBy(tlddoc)
tasks.register('cssMinifyIt', org.gradlewebtools.minify.CssMinifyTask) {
    srcDir = project.file('src/main/resources/META-INF/resources/resources/css')
    dstDir = project.file('build')
    options {
        ignoreMinFiles = true
    }
}
tasks.register('jsMinifyIt', org.gradlewebtools.minify.JsMinifyTask) {
    srcDir = project.file('src/main/resources/META-INF/resources/resources/js')
    dstDir = project.file('build')
    options {
        ignoreMinFiles = true
    }
}
mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()
    coordinates("org.jlab", "smoothness-weblib", version)

    pom {
        name = project.name
        description = project.description
        url = "https://github.com/JeffersonLab/smoothness/"
        licenses {
            license {
                name = "The MIT License"
                url = "https://github.com/JeffersonLab/smoothness/blob/master/LICENSE"
            }
        }
        developers {
            developer {
                id = "slominskir"
                name = "Ryan Slominski"
                url = "https://github.com/slominskir/"
            }
        }
        scm {
            url = "https://github.com/JeffersonLab/smoothness"
            connection = "scm:git:git://github.com/JeffersonLab/smoothness.git"
            developerConnection = "scm:git:ssh://git@github.com/JeffersonLab/smoothness.git"
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}